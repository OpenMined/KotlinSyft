apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        applicationId Versions.demoAppId
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.appVersionCode
        versionName Versions.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties authProperties = new Properties()
        authProperties.load(project.rootProject.file('demo-app/authentication.properties').newDataInputStream())
        buildConfigField 'String', 'SYFT_AUTH_TOKEN', authProperties.getProperty('syft.authToken')
        Properties modelProperties = new Properties()
        modelProperties.load(project.rootProject.file('demo-app/model.properties').newDataInputStream())
        buildConfigField 'String', 'SYFT_MODEL_NAME', modelProperties.getProperty('syft.model_name')
        buildConfigField 'String', 'SYFT_MODEL_VERSION', modelProperties.getProperty('syft.model_version')
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'

    }
}

dependencies {
    implementation CommonDependencies.appCompat
    implementation CommonDependencies.constraintLayout
    implementation CommonDependencies.coreKtx
    implementation CommonDependencies.kotlinSerialization
    implementation CommonDependencies.rxJava
    implementation CommonDependencies.rxAndroid
    implementation CommonDependencies.coroutines

    implementation DemoAppDependencies.mpAndroidChart
    implementation DemoAppDependencies.materialAndroid
    implementation DemoAppDependencies.kotlinJDK
    implementation DemoAppDependencies.workKtx
    implementation DemoAppDependencies.workGCM
    implementation DemoAppDependencies.workRX
    implementation DemoAppDependencies.lifecycleLiveData
    implementation DemoAppDependencies.lifeCycleProcess
    implementation DemoAppDependencies.lifecycleViewModel

    // TODO During the first stages of the project, include the library here. Later on we should use the library from the repository
    implementation project(path: ':syft')
//    implementation 'org.openmined.kotlinsyft:syft:0.1.3'

    implementation SyftlibDependencies.pytorchAndroid
    implementation SyftlibDependencies.torchVisionAndroid

    androidTestImplementation DemoAppDependencies.workTest
    androidTestImplementation CommonDependencies.adxExtJunit
    androidTestImplementation CommonDependencies.espresso
}
